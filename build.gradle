apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    
    compile 'net.bytebuddy:byte-buddy:1.5.9'
    compile 'org.reflections:reflections:0.9.10'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'io.javaslang:javaslang:2.0.4'
    compile 'javax.inject:javax.inject:1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.esotericsoftware:kryo:4.0.0'
}

group = 'io.github.pellse'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Decorator'
        packaging 'jar'
        description 'A library that emulates in Java the Scala Stackable Trait Pattern'
        url 'https://github.com/pellse/decorator'

        scm {
          connection 'scm:git@github.com:pellse/decorator.git'
          developerConnection 'scm:git@github.com:pellse/decorator.git'
          url 'scm:git@github.com:pellse/decorator.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'pellse'
            name 'Sebastien Pelletier'
          }
        }
      }
    }
  }
}
